
import base/nat
import base/order
import base/panic

export base/list
  type Nat
  type Order(t)

  nil    : List(t)
  cat    : List(t) List(t) -- List(t)
  ++     : List(t) List(t) -- List(t)
  cons   : List(t) t -- List(t)
  snoc   : t List(t) -- List(t)

  len? : List(t) -- List(t) Nat
  len  : List(t) -- Nat

  nil?    : List(t) -- List(t) Bool

  map(f: a -- b) : List(a) -- List(b)
  filter(f: b -- b Bool) : List(b) -- List(b)

  for(f: *a b -- *a) : *a List(b) -- *a
  formap(f: *a b -- *a c) : *a List(b) -- *a List(c)
  forfilter(f: *a b -- *a b Bool) : *a List(b) -- *a List(b)

  break : List(t) Nat -- List(t) List(t)
  flatten : List(List(t)) -- List(t)

  lpure : t -- List(t)
  lbind (f: a -- List(b)) : List(a) -- List(b)

  uncons : List(t) -- List(t) t   # panic on empty list
  unsnoc : List(t) -- t List(t)   # panic on empty list
  maximum (ord: Order(a)) : List(a) -- a   # panic on empty list
  minimum (ord: Order(a)) : List(a) -- a   # panic on empty list
end


nil     = _prim_list_nil
cat     = _prim_list_cat
cons    = _prim_list_cons
len     = _prim_list_len znat
map(f) = _prim_list_map(f)
for(f) = _prim_list_for(f)
break  = nint _prim_list_break


formap(f) = dip(nil) for(swap dip(f) swap cons)

filter(f) = forfilter(f)
forfilter(f) = dip(nil) for(swap dip(f) rotr if(cons, drop))

filter(true) == id
filter(false) == drop nil


++    = cat
snoc  = dip(dip(nil) cons) cat
len?  = dup len
nil?  = len? n0 n=



flatten = dip(nil) for(cat)

lpure = dip(nil) cons
lbind(f) = map(f) flatten

cat len == both(len) n+
break cat == drop

map(id) == id
lbind(id) == flatten

uncons = nil? if("uncons on empty list" panic,
  len? n1- znat break 0 _prim_list_at)
unsnoc = nil? if("unsnoc on empty list" panic,
  n1 break dip(0 _prim_list_at))

maximum(ord) = unsnoc for(max(ord))
minimum(ord) = unsnoc for(min(ord))

