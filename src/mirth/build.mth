import base/io
import base/str
import base/unit
import base/list
import base/path
import base/panic
import base/maybe
import base/result
import mirth/pkg
import mirth/mod
import mirth/loc
import mirth/token
import mirth/lexer
import mirth/syntax
import mirth/parser

export mirth/build
  type +IO
  type Path
  buildPath : Path -- Int +IO
end

findMods : Pkg -- List(Mod) +IO
findMods = $(
  dup pkg->path listfiles
  filter(pathHasExtension?("mth"))
  formap(dip(dup) pkgMod) nip
)

buildPkgMods : Maybe(Pkg) List(Mod) -- Int +IO
buildPkgMods = $(
  dip(maybe("no pkg", pkg->str) print)
  cond(
    nil? -> drop "No modules found." print 1,
    for(
      dup mod->str "Reading " swap <> trace
      dup mod->path read
      "Lexing." trace
      tokenize result(
        lexerError->str panic,
        "Parsing." trace
        parseModule runParser
        result(
          syntaxError->str panic,
          getL module->str print
        )
      )
    )
    0
  )
)

buildPkg : Pkg -- Int +IO
buildPkg = dup dip(some) findMods buildPkgMods

buildMod : Mod -- Int +IO
buildMod = dip(none) nil snoc buildPkgMods

buildPath : Path -- Int +IO
buildPath = $(
  cond(
    dup isfile -> pathMod buildMod,
    dup isdir  -> pathPkg buildPkg,
    "mirth: Path does not exist: " swap path->str <> trace 1
  )
)


