
############################### LICENSE ###############################
# This Source Code Form is subject to the terms of the Mozilla Public #
# License, v. 2.0. If a copy of the MPL was not distributed with this #
# file, You can obtain one at http://mozilla.org/MPL/2.0/.            #
#######################################################################

# Tests The Builtins

test(1 dup, 1 1)
test(1 drop,)
test(1 2 swap, 2 1)
test(1 2 5 dip(+), 3 5)
test(let(a,1) let(b,2) let(c,3) a b c, 1 2 3)
test(let(a,1) let(b, let(a,2) a) a b, 1 2)

# Basic Definitions

let(trip, a     -- a a a, dup dup)
let(nip,  a b   -- b,     swap drop)
let(tuck, a b   -- b a b, dup dip(swap))
let(over, a b   -- a b a, dip(dup) swap)
let(rotr, a b c -- c a b, swap dip(swap))
let(rotl, a b c -- b c a, dip(swap) swap)

test(1 trip, 1 1 1)
test(1 2 3 nip, 1 3)
test(1 2 3 tuck, 1 3 2 3)
test(1 2 3 over, 1 2 3 2)
test(1 2 3 rotr, 3 1 2)
test(1 2 3 rotl, 2 3 1)

# Doubled Up

#let(dip2(f), dip(dip(f)))
let(dup2,  a b         -- a b a b,     over over)
let(drop2, a b         --,             drop drop)
let(swap2, a b c d     -- c d a b,     dip(rotr) rotr)
let(trip2, a b         -- a b a b a b, dup2 dup2)
let(nip2,  a b c d     -- c d,         swap2 drop2)
let(tuck2, a b c d     -- c d a b c d, dup2 dip(dip(swap2)))
let(over2, a b c d     -- a b c d a b, dip(dip(dup2)) swap2)
let(rotr2, a b c d e f -- e f a b c d, swap2 dip(dip(swap2)))
let(rotl2, a b c d e f -- c d e f a b, dip(dip(swap2)) swap2)

test(1 2 3 dup2, 1 2 3 2 3)
test(1 2 3 drop2, 1)
test(1 2 3 4 swap2, 3 4 1 2)
test(1 2 trip2, 1 2 1 2 1 2)
test(1 2 3 4 nip2, 3 4)
test(1 2 3 4 tuck2, 3 4 1 2 3 4)
test(1 2 3 4 over2, 1 2 3 4 1 2)
test(1 2 3 4 5 6 rotr2, 5 6 1 2 3 4)
test(1 2 3 4 5 6 rotl2, 3 4 5 6 1 2)

macro(foo, drop quote(10))
test(foo, 10)

